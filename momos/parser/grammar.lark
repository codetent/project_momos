?start : definition+

?definition : state
            | transition

state         : "@state" identifier [ "[" option "]" ]
transition    : "@transition" ( identifier | implicit ) "->" identifier [ "[" trigger "]" ]

trigger       : identifier ["#" identifier] ( "," option )*
option        : flag | pair
pair          : identifier "=" literal
flag          : NOT? NAME

?literal      : integer | float | string
implicit      : "."
identifier    : NAME
string        : STRING
float         : SIGNED_FLOAT
integer       : SIGNED_INT

NOT           : "!"
STRING        : /[\x21\x23-\x26\x28-\x2B\x2D-\x3B\x3E-\x5A\x5C\x5E-\x7E]+/

%import python.NAME

%import common.SIGNED_INT
%import common.SIGNED_FLOAT
%import common.WS

%ignore WS
